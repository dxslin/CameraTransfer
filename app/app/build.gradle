import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
//加载配置文件
Properties localProperties = new Properties()
localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    defaultConfig {
        applicationId "com.slin.camera_transfer"
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(localProperties.getProperty('sign_file'))
            storePassword localProperties.getProperty('store_password')
            keyAlias localProperties.getProperty('key_alias')
            keyPassword localProperties.getProperty('key_password')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'com.github.dxslin:SlinDialog:0.2.3'
}

afterEvaluate{ project ->
    def releaseTask = project.tasks.findByName('assembleRelease')
    releaseTask.doLast{
        copy{
            def versionCode = rootProject.ext.android["versionCode"]
            String versionName = rootProject.ext.android["versionName"]
            String dateStr = new SimpleDateFormat("yyyyMMdd").format(new Date())
            from('\\build\\outputs\\apk\\release'){
                include('**.apk')
            }
            into '\\apk'
            rename('(.+)-(.+)\\.(.+)', 'CameraTransfer$1-' + versionName + '-' + dateStr + '-$2' + + versionCode + '.$3')
        }
    }
}
